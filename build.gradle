buildscript {
    repositories {
        mavenCentral()
        maven { url "http://files.minecraftforge.net/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'org.ajoberstar:gradle-git:0.10.1'
    }
}

plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.2"
    id 'net.franz-becker.gradle-lombok' version '1.5'
}

apply plugin: 'maven-publish'

def build_number = 'CUSTOM'

if (System.getenv('BUILD_NUMBER') != null)
    build_number = System.getenv('BUILD_NUMBER')

def username = 'LendingLibraryDev'

if (project.hasProperty('dev_username'))
    username = "${dev_username}"

group = package_group
archivesBaseName = mod_name
version = "${mc_version}-${mod_version}-${build_number}"

if (project.hasProperty('gitpush'))
    version += "-ss"

import net.franz_becker.gradle.lombok.task.DelombokTask
import org.ajoberstar.grgit.*

def gitHash = 'unknown'

repositories {
}

dependencies {
}

minecraft {
    version = "1.11-13.19.0.2126-1.11.x"
    runDir = "run"

    clientRunArgs += "--username=${username}"
    makeObfSourceJar = false

    if (project.hasProperty('mappings_version'))
        mappings = project.mappings_version
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

jar {
    classifier = ''
    exclude 'tehnut/lib/test/**/*'
    exclude 'assets/**/*'
    from sourceSets.main.output
    manifest.mainAttributes(
            "Built-By": System.getProperty('user.name'),
            "Created-By": "${System.getProperty('java.vm.version')} + (${System.getProperty('java.vm.vendor')})",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Git-Hash": gitHash,
            "Built-On": "${mc_version}-${forge_version}"
    )
}

// Source jar
task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    exclude 'tehnut/lib/test/**/*'
    exclude 'assets/**/*'
    classifier = 'sources'
}

// Javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    exclude 'tehnut/lib/test/**/*'
    exclude 'assets/**/*'
    from sourceSets.main.output
    classifier = 'javadoc'
}

tasks.build.dependsOn javadoc, javadocJar, sourcesJar

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact javadocJar
            artifact sourcesJar
        }
    }
    repositories {
        if (project.hasProperty('maven_repo')) {
            maven { url maven_repo }
        } else {
            mavenLocal()
        }
    }
}